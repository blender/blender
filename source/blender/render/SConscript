#!/usr/bin/env python
#
# ***** BEGIN GPL LICENSE BLOCK *****
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software Foundation,
# Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
#
# The Original Code is Copyright (C) 2006, Blender Foundation
# All rights reserved.
#
# The Original Code is: all of this file.
#
# Contributor(s): Nathan Letwory.
#
# ***** END GPL LICENSE BLOCK *****

Import ('env')

sources = env.Glob('intern/source/*.c')
raysources = env.Glob('intern/raytrace/*.cpp')

incs = [
    'extern/include',
    'intern/include',
    '#/intern/guardedalloc',
    '../blenfont',
    '../blenkernel',
    '../blenlib',
    '../imbuf',
    '../include',
    '../makesdna',
    '../makesrna',
    '../../../intern/mikktspace',
    '../../../intern/smoke/extern',
    ]
incs = ' '.join(incs)

cflags_raytrace = env['CCFLAGS']
cxxflags_raytrace = env['CXXFLAGS']

defs = []
defs_raytrace = []

if env['WITH_BF_SMOKE']:
    defs.append('WITH_SMOKE')

if env['WITH_BF_PYTHON']:
    incs += ' ../python'
    defs.append('WITH_PYTHON')
    if env['BF_DEBUG']:
        defs.append('DEBUG')

if env['OURPLATFORM'] in ('win32-vc', 'win64-vc'):
    if env['WITH_BF_RAYOPTIMIZATION']:
        cflags_raytrace = env['CCFLAGS'] + env['BF_RAYOPTIMIZATION_SSE_FLAGS']
        cxxflags_raytrace = env['CCFLAGS'] + env['BF_RAYOPTIMIZATION_SSE_FLAGS']

if env['OURPLATFORM'] in ('win32-mingw', 'win64-mingw'):
    if env['WITH_BF_RAYOPTIMIZATION']:
        cflags_raytrace = env['CCFLAGS'] + env['BF_RAYOPTIMIZATION_SSE_FLAGS']
        cxxflags_raytrace = env['CXXFLAGS'] + env['BF_RAYOPTIMIZATION_SSE_FLAGS']

if env['OURPLATFORM'] == 'darwin':
    if env['MACOSX_ARCHITECTURE'] in ('i386', 'x86_64') and env['WITH_BF_RAYOPTIMIZATION']:
        cflags_raytrace = env['CFLAGS'] + env['BF_RAYOPTIMIZATION_SSE_FLAGS']
        cxxflags_raytrace = env['CXXFLAGS'] + env['BF_RAYOPTIMIZATION_SSE_FLAGS']
        
if env['OURPLATFORM'] == 'linux':
    if env['WITH_BF_RAYOPTIMIZATION']:
        cflags_raytrace = env['CCFLAGS'] +  env['BF_RAYOPTIMIZATION_SSE_FLAGS']
        cxxflags_raytrace = env['CXXFLAGS'] +  env['BF_RAYOPTIMIZATION_SSE_FLAGS']

if env['OURPLATFORM'] == 'linuxcross':
    if env['WITH_BF_RAYOPTIMIZATION']:
        cflags_raytrace = env['CCFLAGS'] + env['BF_RAYOPTIMIZATION_SSE_FLAGS']
        cxxflags_raytrace = env['CCFLAGS'] + env['BF_RAYOPTIMIZATION_SSE_FLAGS']

if env['WITH_BF_QUICKTIME']:
    defs.append('WITH_QUICKTIME')
    incs += ' ../quicktime ' + env['BF_QUICKTIME_INC']

if env['WITH_BF_OPENEXR']:
    defs.append('WITH_OPENEXR')

if env['WITH_BF_FREESTYLE']:
    incs += ' ../freestyle'
    defs.append('WITH_FREESTYLE')

if env['WITH_BF_GAMEENGINE']:
    defs.append('WITH_GAMEENGINE')

if env['WITH_BF_INTERNATIONAL']:
    defs.append('WITH_INTERNATIONAL')

if env['WITH_BF_CYCLES'] and env['WITH_BF_CYCLES_DEBUG']:
    defs.append('WITH_CYCLES_DEBUG')

if env['OURPLATFORM'] in ('win32-vc', 'win32-mingw', 'linuxcross', 'win64-vc', 'win64-mingw'):
    incs += ' ' + env['BF_PTHREADS_INC']

#
# HACK: To fix problem with error 'MMX instruction set not enabled' from mmintrin.h
#
if env['OURPLATFORM'] == 'linuxcross':
    defs.append('__SSE__')
    defs_raytrace.append('__MMX__')

if env['WITH_BF_RAYOPTIMIZATION']:
    defs.append('__SSE__')
    defs_raytrace.append('__SSE__')

env.BlenderLib ( libname = 'bf_render', sources = sources, includes = Split(incs), defines=defs, libtype='core', priority=145 )
env.BlenderLib ( libname = 'bf_render_raytrace', sources = raysources, includes = Split(incs), defines=defs_raytrace, libtype='core', priority=145, compileflags=cflags_raytrace, cxx_compileflags=cxxflags_raytrace )
