#!/usr/bin/python
Import ('env')

sources = env.Glob('intern/*.c')

incs = '. ../makesdna #/intern/guardedalloc #/intern/memutil ../blenlib'
incs += ' ../avi ../quicktime ../blenkernel'

incs += ' ' + env['BF_JPEG_INC']
incs += ' ' + env['BF_PNG_INC']
incs += ' ' + env['BF_TIFF_INC']
incs += ' ' + env['BF_ZLIB_INC']

defs = []

if env['WITH_BF_VERSE']:
	defs.append('WITH_VERSE')
	incs += ' ' + env['BF_VERSE_INCLUDE']
	
if env['WITH_BF_OPENEXR'] == 1:
    defs.append('WITH_OPENEXR')

if env['WITH_BF_DDS'] == 1:
    defs.append('WITH_DDS')

if env['WITH_BF_FFMPEG'] == 1:
    defs.append('WITH_FFMPEG')
    incs += ' ' + env['BF_FFMPEG_INC']

if env['WITH_BF_OPENJPEG'] == 1:
    defs.append('WITH_OPENJPEG')
    incs += ' ' + env['BF_OPENJPEG_INC']

if env['WITH_BF_REDCODE'] == 1:
    defs.append('WITH_REDCODE')
    incs += ' ' + env['BF_REDCODE_INC']

if env['WITH_BF_QUICKTIME']==1:
	incs += ' ' + env['BF_QUICKTIME_INC']
	defs.append('WITH_QUICKTIME')

env.BlenderLib ( libname = 'bf_imbuf', sources = sources, includes = Split(incs), defines = defs, libtype=['core','player'], priority = [80, 40] )
