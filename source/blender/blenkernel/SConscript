#!/usr/bin/python
Import ('env')

sources = env.Glob('intern/*.c')

incs = '. #/intern/guardedalloc ../include ../blenlib ../makesdna'
incs += ' ../render/extern/include #/intern/decimation/extern'
incs += ' ../imbuf ../ikplugin ../avi #/intern/elbeem/extern ../nodes'
incs += ' #/intern/iksolver/extern ../blenloader'
incs += ' #/extern/bullet2/src'
incs += ' #/intern/bmfont'
incs += ' #/intern/opennl/extern'
incs += ' ../gpu #/extern/glew/include'

incs += ' ' + env['BF_OPENGL_INC']
incs += ' ' + env['BF_ZLIB_INC']

defs = []

if not env['WITH_BF_PYTHON']:
	defs.append('DISABLE_PYTHON')
else:
	incs += ' ../python'
	incs += ' ' + env['BF_PYTHON_INC']
	if env['BF_DEBUG']:
		defs.append('_DEBUG')

if env['WITH_BF_QUICKTIME']:
	incs += ' ../quicktime'

if env['WITH_BF_SDL']:
	incs += ' ' + env['BF_SDL_INC']
else:
	defs.append('DISABLE_SDL')

if env['WITH_BF_INTERNATIONAL']:
	defs.append('WITH_FREETYPE2')

if env['WITH_BF_VERSE']:
	defs.append('WITH_VERSE')
	incs += ' ' + env['BF_VERSE_INCLUDE']

if env['WITH_BF_VERSE']:
	defs.append('WITH_VERSE')
	
if env['WITH_BF_OPENEXR']:
	defs.append('WITH_OPENEXR')

if env['WITH_BF_OPENJPEG']:
	defs.append('WITH_OPENJPEG')

if env['WITH_BF_DDS']:
	defs.append('WITH_DDS')

if env['WITH_BF_FFMPEG']:
	defs.append('WITH_FFMPEG')
	incs += ' ' + env['BF_FFMPEG_INC']

if env['WITH_BF_QUICKTIME']:
	defs.append('WITH_QUICKTIME')
	incs += ' ' + env['BF_QUICKTIME_INC']

if env['WITH_BF_BULLET']:
	defs.append('WITH_BULLET')

if env['BF_NO_ELBEEM']:
	defs.append('DISABLE_ELBEEM')

if env['WITH_BF_PLAYER']:
	SConscript(['bad_level_call_stubs/SConscript'])

env.BlenderLib ( libname = 'bf_blenkernel', sources = sources, includes = Split(incs), defines = defs, libtype=['core','player'], priority = [65, 20] )
