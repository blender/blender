#!/usr/bin/python
Import ('env')

sources = env.Glob('intern/*.c')

incs = '. #/intern/guardedalloc ../include ../blenlib ../makesdna'
incs += ' ../python ../render/extern/include #/intern/decimation/extern'
incs += ' ../imbuf ../avi #/intern/elbeem/extern'
incs += ' #/intern/iksolver/extern ../blenloader ../quicktime'

incs += ' ' + env['BF_OPENGL_INC']
incs += ' ' + env['BF_ZLIB_INC']
incs += ' ' + env['BF_SDL_INC']

defs = ''

if env['WITH_BF_INTERNATIONAL'] == 1:
    defs += 'WITH_FREETYPE2'

if env['WITH_BF_OPENEXR'] == 1:
    defs += ' WITH_OPENEXR'

if env['WITH_BF_FFMPEG'] == 1:
    defs += ' WITH_FFMPEG'

if env['WITH_BF_QUICKTIME'] == 1:
    defs += ' WITH_QUICKTIME'
    incs += ' ' + env['BF_QUICKTIME_INC']

defs += ' WITH_CCGSUBSURF'

if env['WITH_BF_PLAYER']:
    SConscript(['bad_level_call_stubs/SConscript'])

env.BlenderLib ( libname = 'blenkernel', sources = sources, includes = Split(incs), defines = Split(defs), libtype='core', priority = 65 )
