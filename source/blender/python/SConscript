#!/usr/bin/env python
#
# ***** BEGIN GPL LICENSE BLOCK *****
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software Foundation,
# Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
#
# The Original Code is Copyright (C) 2006, Blender Foundation
# All rights reserved.
#
# The Original Code is: all of this file.
#
# Contributor(s): Nathan Letwory.
#
# ***** END GPL LICENSE BLOCK *****

# TODO, split into 3 files.

Import ('env')

incs = [
    '.',
    '#/intern/guardedalloc',
    '#/intern/memutil',
    env['BF_GLEW_INC'],
    '#/intern/glew-mx',
    '#/intern/audaspace/intern',
    '#/intern/cycles/blender',
    '../blenfont',
    '../blenkernel',
    '../blenlib',
    '../blenloader',
    '../bmesh',
    '../editors/include',
    '../gpu',
    '../imbuf',
    '../makesdna',
    '../makesrna',
    '../nodes',
    '../render/extern/include',
    '../windowmanager',
    env['BF_PYTHON_INC'],
    ]
incs = ' '.join(incs)

is_debug = (env['OURPLATFORM'] in ('win32-mingw', 'win32-vc','win64-vc', 'win64-mingw') and env['BF_DEBUG'])

# bmesh
defs = []

sources = env.Glob('bmesh/*.c')
env.BlenderLib( libname = 'bf_python_bmesh', sources = Split(sources), includes = Split(incs), defines = defs, libtype = ['core','player'], priority = [362,165])

# generic
defs = env['BF_GL_DEFINITIONS']

if is_debug:
    defs.append('_DEBUG')

sources = env.Glob('generic/*.c')
env.BlenderLib( libname = 'bf_python_ext', sources = Split(sources), includes = Split(incs), defines = defs, libtype = ['core','player'], priority = [363,165]) # ketsji is 360


# mathutils
defs = []

sources = env.Glob('mathutils/*.c')
env.BlenderLib( libname = 'bf_python_mathutils', sources = Split(sources), includes = Split(incs), defines = defs, libtype = ['core','player'], priority = [362,165])


# bpy
defs = env['BF_GL_DEFINITIONS']

if is_debug:
    defs.append('_DEBUG')

if env['WITH_BF_PYTHON_SAFETY']:
    defs.append('WITH_PYTHON_SAFETY')

if env['BF_BUILDINFO']:
    defs.append('BUILD_DATE')


# Audaspace is always on currently

if env['WITH_BF_BULLET']:
    defs.append('WITH_BULLET')

# AVI is always on currently

if env['WITH_BF_FFMPEG']:
    defs.append('WITH_FFMPEG')
    incs += ' ' + env['BF_FFMPEG_INC']

if env['WITH_BF_QUICKTIME']:
    defs.append('WITH_QUICKTIME')

if env['WITH_BF_SNDFILE']:
    defs.append('WITH_SNDFILE')

if env['WITH_BF_COMPOSITOR']:
    defs.append('WITH_COMPOSITOR')

if env['WITH_BF_CYCLES']:
    defs.append('WITH_CYCLES')

if env['WITH_BF_CYCLES_OSL']:
    defs.append('WITH_CYCLES_OSL')

if env['WITH_BF_FREESTYLE']:
    incs += ' ../freestyle/intern/python'
    defs.append('WITH_FREESTYLE')

if env['WITH_BF_GAMEENGINE']:
    defs.append('WITH_GAMEENGINE')

if env['WITH_BF_CINEON']:
    defs.append('WITH_CINEON')

if env['WITH_BF_DDS']:
    defs.append('WITH_DDS')

if env['WITH_BF_FRAMESERVER']:
    defs.append('WITH_FRAMESERVER')

if env['WITH_BF_HDR']:
    defs.append('WITH_HDR')

if env['WITH_BF_OPENEXR']:
    defs.append('WITH_OPENEXR')

if env['WITH_BF_OPENJPEG']:
    defs.append('WITH_OPENJPEG')

if env['WITH_BF_REDCODE']:
    defs.append('WITH_REDCODE')

if env['WITH_BF_TIFF']:
    defs.append('WITH_TIFF')

# NDof is always on currently

if env['WITH_BF_INTERNATIONAL']:
    defs.append('WITH_INTERNATIONAL')

if env['WITH_BF_OPENAL']:
    defs.append('WITH_OPENAL')

if env['WITH_BF_SDL']:
    defs.append('WITH_SDL')

if env['WITH_BF_JACK']:
    defs.append('WITH_JACK')

if env['WITH_BF_LIBMV']:
    defs.append('WITH_LIBMV')

if env['WITH_BF_BOOLEAN']:
    defs.append('WITH_MOD_BOOLEAN')

if env['WITH_BF_FLUID']:
    defs.append('WITH_MOD_FLUID')

if env['WITH_BF_OCEANSIM']:
    defs.append('WITH_OCEANSIM')

if env['WITH_BF_REMESH']:
    defs.append('WITH_MOD_REMESH')

if env['WITH_BF_SMOKE']:
    defs.append('WITH_SMOKE')

if env['WITH_BF_COLLADA']:
    defs.append('WITH_COLLADA')

if env['WITH_BF_OCIO']:
    defs.append('WITH_OCIO')
    incs += ' ' + '#/intern/opencolorio'

if env['WITH_BF_OIIO']:
    defs.append('WITH_OPENIMAGEIO')
    incs += ' ../imbuf/intern/oiio'

if env['WITH_BF_PLAYER']:
    defs.append('WITH_PLAYER')
    

if env['OURPLATFORM'] in ('win32-vc', 'win32-mingw', 'win64-mingw', 'linuxcross', 'win64-vc'):
    incs += ' ' + env['BF_PTHREADS_INC']

sources = env.Glob('intern/*.c')
env.BlenderLib( libname = 'bf_python', sources = Split(sources), includes = Split(incs), defines = defs, libtype = ['core'], priority = [361])
