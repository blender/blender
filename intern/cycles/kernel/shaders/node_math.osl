/*
 * Copyright 2011-2013 Blender Foundation
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

#include "stdosl.h"

float safe_divide(float a, float b)
{
	float result;

	if (b == 0.0)
		result = 0.0;
	else
		result = a / b;
	
	return result;
}

float safe_modulo(float a, float b)
{
	float result;

	if (b == 0.0)
		result = 0.0;
	else
		result = fmod(a, b);
	
	return result;
}

float safe_sqrt(float a)
{
	float result;

	if (a > 0.0)
		result = sqrt(a);
	else
		result = 0.0;

	return result;
}

float safe_log(float a, float b)
{
	if (a < 0.0 || b < 0.0)
		return 0.0;
	
	return log(a) / log(b);
}

shader node_math(
	string type = "add",
	int use_clamp = 0,
	float Value1 = 0.0,
	float Value2 = 0.0,
	output float Value = 0.0)
{
	/* OSL asin, acos, pow check for values that could give rise to nan */

	if (type == "add")
		Value = Value1 + Value2;
	else if (type == "subtract")
		Value = Value1 - Value2;
	else if (type == "multiply")
		Value = Value1 * Value2;
	else if (type == "divide")
		Value = safe_divide(Value1, Value2);
	else if (type == "sine")
		Value = sin(Value1);
	else if (type == "cosine")
		Value = cos(Value1);
	else if (type == "tangent")
		Value = tan(Value1);
	else if (type == "arcsine")
		Value = asin(Value1);
	else if (type == "arccosine")
		Value = acos(Value1);
	else if (type == "arctangent")
		Value = atan(Value1);
	else if (type == "power")
		Value = pow(Value1, Value2);
	else if (type == "logarithm")
		Value = safe_log(Value1, Value2);
	else if (type == "minimum")
		Value = min(Value1, Value2);
	else if (type == "maximum")
		Value = max(Value1, Value2);
	else if (type == "round")
		Value = floor(Value1 + 0.5);
	else if (type == "less_than")
		Value = Value1 < Value2;
	else if (type == "greater_than")
		Value = Value1 > Value2;
	else if (type == "modulo")
		Value = safe_modulo(Value1, Value2);
	else if (type == "absolute")
		Value = fabs(Value1);
	else if (type == "arctan2")
		Value = atan2(Value1, Value2);
	else if (type == "floor")
		Value = floor(Value1);
	else if (type == "ceil")
		Value = ceil(Value1);
	else if (type == "fract")
		Value = Value1 - floor(Value1);
	else if (type == "sqrt")
		Value = safe_sqrt(Value1);

	if (use_clamp)
		Value = clamp(Value, 0.0, 1.0);
}

