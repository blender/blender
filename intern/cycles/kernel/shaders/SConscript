#!/usr/bin/env python
#
# ***** BEGIN GPL LICENSE BLOCK *****
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software Foundation,
# Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301, USA.
#
# The Original Code is Copyright (C) 2011, Blender Foundation
# All rights reserved.
#
# The Original Code is: all of this file.
#
# Contributor(s): Nathan Letwory.
#
# ***** END GPL LICENSE BLOCK *****

import sys
import os
import Blender as B

def normpath(path):
    return os.path.abspath(os.path.normpath(path))

Import ('env')

oso_files = []

if env['WITH_BF_CYCLES_OSL']:
    shaders = env.Clone()

    # osl compiler
    osl_compiler = File(env.subst(env['BF_OSL_COMPILER'])).abspath

    # build directory
    root_build_dir = normpath(env['BF_BUILDDIR'])
    build_dir = os.path.join(root_build_dir, 'intern/cycles/kernel/shaders')

    # source directories and files
    source_dir = Dir('.').srcnode().path

    # dependencies
    dependencies = shaders.Glob('*.h')

    for f in os.listdir(source_dir):
        if f.endswith('.osl'):
            osl_file = os.path.join(source_dir, f)
            oso_file = os.path.join(build_dir, f.replace('.osl', '.oso'))

            command = "\"%s\" -q -O2 -I\"%s\" -o \"%s\" \"%s\"" % (osl_compiler, source_dir, oso_file, osl_file)

            shaders.Command(oso_file, f, command)
            shaders.Depends(oso_file, [f] + dependencies)

            oso_files.append(oso_file)


Return('oso_files')
