/*
 * Copyright 2011-2013 Blender Foundation
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License
 */

#include "stdosl.h"
#include "node_fresnel.h"

shader node_glass_bsdf(
	color Color = 0.8,
	string distribution = "Sharp",
	float Roughness = 0.2,
	float IOR = 1.45,
	normal Normal = N,
	output closure color BSDF = 0)
{
	float f = max(IOR, 1e-5);
	float eta = backfacing() ? 1.0 / f: f;
	float cosi = dot(I, Normal);
	float Fr = fresnel_dielectric_cos(cosi, eta);

	if (distribution == "Sharp")
		BSDF = Color * (Fr * reflection(Normal) + (1.0 - Fr) * refraction(Normal, eta));
	else if (distribution == "Beckmann")
		BSDF = Color * (Fr * microfacet_beckmann(Normal, Roughness) +
		                (1.0 - Fr) * microfacet_beckmann_refraction(Normal, Roughness, eta));
	else if (distribution == "GGX")
		BSDF = Color * (Fr * microfacet_ggx(Normal, Roughness) +
		                (1.0 - Fr) * microfacet_ggx_refraction(Normal, Roughness, eta));
}

