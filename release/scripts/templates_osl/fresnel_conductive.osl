/*
 * Copyright 2011-2014 Blender Foundation
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License
 */

color fresnel_conductor(float cosi, color eta, color k)
{
	color cosi2 = color(cosi*cosi);
	color one = color(1, 1, 1);
	color tmp_f = eta * eta + k * k;
	color tmp = tmp_f * cosi2;
	color Rparl2 = (tmp - (2.0 * eta * cosi) + one) /
					(tmp + (2.0 * eta * cosi) + one);
	color Rperp2 = (tmp_f - (2.0 * eta * cosi) + cosi2) /
					(tmp_f + (2.0 * eta * cosi) + cosi2);
	return (Rparl2 + Rperp2) * 0.5;
}

shader node_fresnel_conductive(
	color n = color(0.084136, 0.410708, 1.472421),
	color k = color(4.018579, 2.363371, 1.607574),
	normal Normal = N,
	output color Color = color(0.8, 0.8, 0.8))
{
	float cosi = dot(I, Normal);
	Color = fresnel_conductor(cosi, n, k);
}
